; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "BeanieDeploy"
#define MyAppVersion "0.9"
#define MyAppPublisher "M Zidane"
#define MyAppURL "https://zidane.dev/"
#define SourcePath "..\"
[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B35596A1-4B25-43BB-9581-291E681EEA7D}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
VersionInfoDescription= {#MyAppName}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={%USERPROFILE}\AppData\Local\Temp\BeanieDeploy\win2linux
DisableDirPage=yes
DisableProgramGroupPage=yes
DisableReadyPage=yes
DisableFinishedPage=yes
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir={#SourcePath}\output
OutputBaseFilename={#MyAppName}-v{#MyAppVersion}-Bundled-Win64
SetupIconFile="{#SourcePath}\src\resources\style\app-icon.ico"
Compression=lzma
Uninstallable=no
SolidCompression=yes
AllowCancelDuringInstall=no
WizardStyle=modern
WizardSizePercent=150,100
WizardResizable=no
WizardSmallImageFile=beanie-icon.bmp

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "{#SourcePath}\interpreter\*"; DestDir: "{app}\interpreter\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SourcePath}\src\*"; DestDir: "{app}\src\"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Run]
Filename: "{app}\interpreter\python\pythonw.exe"; Parameters: "../../src/main.py"

[LangOptions]
DialogFontSize=11
WelcomeFontName=Verdana
;WelcomeFontSize=10
TitleFontName=Arial
TitleFontSize=30
CopyrightFontName=Arial
;CopyrightFontSize=13

[Messages]
WizardInstalling=Loading - {#MyAppName} v{#MyAppVersion}
InstallingLabel=Please wait while extracting temporary files on your computer.
StatusExtractFiles=

WizardReady=Ready to Extract
ReadyLabel1=Temporary files will be extracted
ReadyLabel2a= 
ReadyLabel2b= 

StatusRunProgram=Almost there...

SetupAppTitle={#MyAppName}
SetupWindowTitle={#MyAppName}
AboutSetupMessage=%1 v%2%n%3%n%n%1 home page:%n%4

[Code]
const
  BN_CLICKED = 0;
  WM_COMMAND = $0111;
  CN_BASE = $BC00;
  CN_COMMAND = CN_BASE + WM_COMMAND;

procedure CurPageChanged(CurPageID: Integer);
var
  Param: Longint;
begin
  { if we are on the ready page, then... }
  if CurPageID = wpReady then
  begin
    { the result of this is 0, just to be precise... }
    Param := 0 or BN_CLICKED shl 16;
    { post the click notification message to the next button }
    PostMessage(WizardForm.NextButton.Handle, CN_COMMAND, Param, 0);
  end;
end;

procedure InitializeWizard;
begin
  WizardForm.FilenameLabel.Visible := False;
end;